dnl Process this file with autoconf to produce a configure script.
dnl
dnl  Copyright (C) 2002,2003,2004,2005,2006,2007,2008 Thien-Thi Nguyen
dnl  Portions Copyright (C) 1998 Ian Grant
dnl
dnl  This file is part of Guile-PG.
dnl
dnl  Guile-PG is free software; you can redistribute it and/or modify it
dnl  under the terms of the GNU General Public License as published by
dnl  the Free Software Foundation; either version 3, or (at your
dnl  option) any later version.
dnl
dnl  Guile-PG is distributed in the hope that it will be useful, but
dnl  WITHOUT ANY WARRANTY; without even the implied warranty of
dnl  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl  General Public License for more details.
dnl
dnl  You should have received a copy of the GNU General Public License
dnl  along with Guile-PG; see the file COPYING.  If not, write to the
dnl  Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
dnl  Boston, MA  02110-1301  USA

AC_INIT([Guile-PG],[0.36],[ttn@gnuvola.org])
AC_CONFIG_AUX_DIR([build-aux])
AM_INIT_AUTOMAKE

AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

AC_DISABLE_STATIC	dnl module only
AC_LIBTOOL_TAGS([])
AC_PROG_LIBTOOL

AC_PROG_CC

AC_STDC_HEADERS

AC_HAVE_HEADERS(string.h stdio.h stdlib.h malloc.h unistd.h)

AC_CHECK_FUNCS(tmpfile)

##----------------------------------------------------------------------------
## Guile

GUILE_PROGS
GUILE_FLAGS

GUILE_SITE_DIR
GUILE_LIBSITE_DIR

saved_CPPFLAGS="$CPPFLAGS"	# restoration below
saved_LDFLAGS="$LDFLAGS"
CPPFLAGS="$GUILE_CFLAGS $CPPFLAGS"
LDFLAGS="$GUILE_LDFLAGS $LDFLAGS"

AC_GUILE_PG_BCOMPAT
AC_GUILE_PG_FCOMPAT

CPPFLAGS="$saved_CPPFLAGS"
LDFLAGS="$saved_LDFLAGS"

AH_BOTTOM([
#include "bcruft.h"
#include "fcruft.h"
])

##----------------------------------------------------------------------------
## PostgreSQL

## This little hack allows the tests for libpq etc. to be overridden by
## setting the environment variables PQ_CPPFLAGS and PQ_LDFLAGS.  This is
## useful e.g. when running 'Make distcheck', which runs ./configure.

if test "x$PQ_LDFLAGS" = "x" -a "x$PQ_CPPFLAGS" = "x" ; then
PQ_FLAGS
fi

saved_CPPFLAGS="$CPPFLAGS"                  # restoration below
saved_LDFLAGS="$LDFLAGS"
CPPFLAGS="$PQ_CPPFLAGS $CPPFLAGS"
LDFLAGS="$PQ_LDFLAGS $LDFLAGS"

AC_CHECK_LIB(pq, PQconnectdb, ,[
  AC_MSG_NOTICE(CPPFLAGS: $CPPFLAGS)
  AC_MSG_NOTICE(LDFLAGS: $LDFLAGS)
  AC_MSG_ERROR([cannot find libpq on the system])
])
dnl In this check, PQprotocolVersion begins the "Protocol 3.0" functions.
AC_CHECK_FUNCS([ \
  PQfmod PQbinaryTuples PQbackendPID \
  PQresultErrorMessage PQpass PQputnbytes \
  PQoidValue PQsetnonblocking PQisnonblocking \
  PQprotocolVersion PQfreemem \
])

AC_CHECK_DECL([InvalidOid],,
              [pg_ac_need_invalidoid=true],
              [[#include "libpq-fe.h"]])

if test x"$pg_ac_need_invalidoid" = xtrue ; then
  AC_CHECK_DECL([InvalidOid],[
    pg_ac_need_invalidoid=false
    AC_DEFINE(INVALIDOID_HEADER, ["postgresql/postgres.h"],
              [Header that declares InvalidOid if not libpq-fe.h.])
    ],,[[#include "postgresql/postgres.h"]])
fi

## Add other locations to check here.

if test "x$pg_ac_need_invalidoid" = xtrue ; then
  AC_MSG_ERROR([cannot find declaration for InvalidOid])
fi

CPPFLAGS="$saved_CPPFLAGS"
LDFLAGS="$saved_LDFLAGS"

AC_CHECK_PROGS([DROPDB], [dropdb destroydb], [no],)
if test "$DROPDB" = no ; then
   AC_MSG_WARN([could not find dropdb or destroydb; "make check" will fail])
fi
AC_CHECK_PROGS([CREATEDB], [createdb], [no])
if test "$CREATEDB" = no ; then
   AC_MSG_WARN([could not find createdb; "make check" will fail])
fi

##----------------------------------------------------------------------------
## Etc

SET_SOFIXFLAGS
AC_PROG_INSTALL
AC_PROG_LN_S

## If we're using GCC, ask for aggressive warnings.
case "$GCC" in
  yes )
#   CFLAGS="$CFLAGS -Wall -Wpointer-arith -Wmissing-prototypes" ;;
    CFLAGS="$CFLAGS -Wall -Wmissing-prototypes" ;;
esac

AC_CONFIG_FILES([
  Makefile
  scm/Makefile
  doc/Makefile
  test/Makefile
])

AC_GUILE_PG_CONFIG_SCRIPT([test/drop.sh])
AC_GUILE_PG_CONFIG_SCRIPT([test/create.sh])
AC_GUILE_PG_CONFIG_SCRIPT([test/runtest])

AC_CONFIG_COMMANDS([twerp-prep],[
  if echo "$ac_cs_version" | grep -q options.*--enable-maintainer-mode ; then
    if guile-tools | grep -q prep-Ptexi ; then
      guile-tools prep-Ptexi doc/Makefile `cat $srcdir/doc/.twerp-list`
    else
      AC_MSG_WARN([[automatic doc rebuild not supported; you need to]])
      AC_MSG_WARN([[edit doc/guile-pg.texi manually if source changes]])
    fi
  fi
])

GUILE_MODULE_CATALOG_PREP

AC_OUTPUT

dnl configure.in ends here
